plugins {
	id 'com.android.application'
}

android {
	compileSdk 31

	defaultConfig {
		applicationId "eu.vcmi.vcmi"
		minSdk 21
		targetSdk 31
		versionCode 1
		versionName "0.99"

		externalNativeBuild {
			cmake {
				arguments "-DANDROID_STL=${VCMI_STL_VERSION}",
					"-DANDROID_NATIVE_API_LEVEL=${VCMI_PLATFORM}",
					"-DANDROID_TOOLCHAIN=clang",
					"-DVCMI_ROOT=${PROJECT_PATH_BASE}"
				cppFlags "-std=c++11", "-frtti", "-fexceptions", "-Wno-switch"
			}
		}
		ndk {
			abiFilters = new HashSet<>()
			abiFilters.addAll(VCMI_ABIS)
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	tasks.withType(JavaCompile) {
		options.compilerArgs += ["-Xlint:deprecation"]
	}
	
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	
	externalNativeBuild {
		cmake {
			path file('cmake-scripts/CMakeLists.txt')
		}
	}
	
	buildFeatures {
		viewBinding true
	}
	
	flavorDimensions "vcmi"
	productFlavors {
		VcmiOnly {
			dimension "vcmi"
			externalNativeBuild {
				cmake {
					targets "vcmi",
							"vcmiserver",
							"vcmiclient"
				}
			}
		}
		LibsOnly {
			dimension "vcmi"
			externalNativeBuild {
				cmake {
					targets "boost-datetime",
							"boost-system",
							"boost-filesystem",
							"boost-locale",
							"boost-program-options",
							"boost-thread",
							"fl-shared",
							"minizip"
				}
			}
		}
		AllTargets {
			dimension "vcmi"
			externalNativeBuild {
				cmake {
					targets "boost-datetime",
							"boost-system",
							"boost-filesystem",
							"boost-locale",
							"boost-program-options",
							"boost-thread",
							"fl-shared",
							"minizip",
							"vcmi",
							"vcmiserver",
							"vcmiclient"
				}
			}
		}
	}
}

dependencies {
	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'com.google.android.material:material:1.3.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
}